dnl configure.in.in
dnl
dnl xfce4-sensors-plugin - Sensors plugin for xfce4-panel
dnl
dnl 2003,2004 Benedikt Meurer <benny@xfce.org>
dnl 2007 Benedikt Meurer <benny@xfce.org>, Fabian Nowak <timystery@arcor.de>
dnl

m4_define([sensors_version],[0.11.0])

AC_INIT([xfce4-sensors-plugin], [sensors_version()], [timystery@arcor.de])
AC_PREREQ([2.50])
AC_CANONICAL_TARGET()

SENSORS_VERSION=sensors_version()
AM_INIT_AUTOMAKE([xfce4-sensors-plugin], [$SENSORS_VERSION])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE

dnl Check for UNIX variants
AC_AIX
AC_ISC_POSIX
AC_MINIX

dnl Check for basic programs
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LIBTOOL
AC_PROG_INTLTOOL

dnl Check for standard header files
AC_HEADER_STDC

dnl Check for libsensors depending on whether it is disabled upon user request
AC_ARG_ENABLE([libsensors], [AC_HELP_STRING([--disable-libsensors], [Do not use libsensors to read a vast amount of sensors on your mainboards @<:@default=no@:>@])],
[
	HAVE_LIBSENSORS=0
], [
	AC_CHECK_LIB([sensors], [sensors_get_detected_chips],
	[
	  AC_DEFINE([HAVE_LIBSENSORS], [1], [Define to 1 if libsensors is found])
	  HAVE_LIBSENSORS=1
	],
	[
		HAVE_LIBSENSORS=0
	])
])

dnl Check for hddtemp depending on whether it is disabled upon user request
AC_ARG_ENABLE([hddtemp], [AC_HELP_STRING([--disable-hddtemp], [Do not use hddtemp to read your harddisk temperature @<:@default=no@:>@])],
[
	HAVE_HDDTEMP=0
],
[
	AC_CHECK_PROG([hddtemp], [hddtemp], [1])
	if test "$hddtemp" = 1; then
		AC_DEFINE([HAVE_HDDTEMP], [1], [Define to 1 if hddtemp is desired])
		HAVE_HDDTEMP=1
	else
		HAVE_HDDTEMP=0
	fi
])

dnl Check for interface for /proc/acpi to be used
AC_ARG_ENABLE([procacpi], [AC_HELP_STRING([--disable-procacpi], [Do not use /proc/acpi to read your CPU temperature @<:@default=no@:>@])],
[
	HAVE_ACPI=0
],
[
	AC_CHECK_FILE([/proc/acpi/info],
	[
		AC_DEFINE([HAVE_ACPI], [1], [Define to 1 if proc/acpi is found])
		HAVE_ACPI=1
	],
	[
		HAVE_ACPI=0
	])
])

dnl Get operating system information
KERNEL=`uname -s`
if test "$KERNEL" = "Linux"; then
	VERSION=`uname -r`
	if test "$VERSION" = "2.6"; then
		AC_DEFINE([HAVE_LINUX26], [1], [Set this to 1 if using Linux 2.6 or newer])
	else
		AC_DEFINE([HAVE_LINUX24], [1], [Set this to 1 if using Linux 2.4 and older])
	fi
OS="$KERNEL"
AC_DEFINE([OS], ["$OS"], [Set this to the name of the oeprating system.])
fi

dnl Substitute in Makefile
dnl AC_SUBST(OS)
dnl where's that crap from?

AC_CHECK_FUNCS([gethostname])
AC_CHECK_FUNCS([memset])
AC_CHECK_FUNCS([socket])
AC_CHECK_FUNCS([strstr])
AC_CHECK_FUNCS([malloc])
AC_CHECK_FUNCS([bzero])
AC_CHECK_HEADERS([arpa/inet.h])
AC_CHECK_HEADERS([netinet/in.h])
AC_CHECK_HEADERS([stdlib.h])
AC_CHECK_HEADERS([string.h])
AC_CHECK_HEADERS([sys/ioctl.h])
AC_CHECK_HEADERS([sys/param.h])
AC_CHECK_HEADERS([sys/socket.h])
AC_CHECK_HEADERS([sys/time.h])
AC_CHECK_HEADERS([unistd.h])
AC_CHECK_HEADERS([sys/sockio.h])
AC_HEADER_SYS_WAIT
AC_PROG_GCC_TRADITIONAL
AC_TYPE_SIZE_T

AC_CHECK_LIB(kstat, kstat_open, SOLLIBS="-lkstat -lsocket", SOLLIBS="")
AC_CHECK_LIB(nsl, kstat_open, SOLLIBS="$SOLLIBS -linet_ntop", SOLLIBS="$SOLLIBS")
AC_SUBST(SOLLIBS)

dnl configure the panel plugin
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-1.0], [4.4.0])

dnl I18n support
XDT_I18N([@LINGUAS@])

dnl Check for debugging support
XDT_FEATURE_DEBUG()

AC_OUTPUT([
Makefile
xfce4-sensors-plugin.spec
panel-plugin/Makefile
icons/Makefile
po/Makefile.in
])


dnl ***************************
dnl *** Print configuration ***
dnl ***************************
echo
echo "Build Configuration:"
echo
echo "Host system:	$OS"
echo
echo "The following sensor interfaces will be built into the plugin:"
if test "$HAVE_HDDTEMP" = 1; then
echo "*  hddtemp"
fi
if test "$HAVE_LIBSENSORS" = 1; then
echo "*  libsensors"
fi
if test "$HAVE_ACPI" = 1; then
echo "*  /proc/acpi"
fi
echo
